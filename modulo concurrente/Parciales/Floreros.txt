{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores(ES f:numero)
  comenzar
    mientras HayFlorEnLaEsquina  
      tomarFlor
      f:=f+1
  fin
  proceso habilitarUnBot(E id:numero)
  comenzar
    si id = 1
      EnviarMensaje(V,r1)
    sino
      si id = 2
        EnviarMensaje(V,r2)
      sino
        EnviarMensaje(V,r3)
  fin
  proceso enviarEsquina(E av:numero; E ca:numero; E id:numero)
  comenzar
    si id = 1
      EnviarMensaje(av,r1)
      EnviarMensaje(ca,r1)
    sino
      si id =2 
        EnviarMensaje(av,r2)
        EnviarMensaje(ca,r2)
      sino
        EnviarMensaje(av,r3)
        EnviarMensaje(ca,r3)
  fin
areas
  c: AreaPC(40,40,60,60)
  c1: AreaPC(10,10,10,10)
  
  a0: AreaP(4,1,4,1)
  a1: AreaP(1,1,1,1)
  a2: AreaP(2,1,2,1)
  a3: AreaP(3,1,3,1)
robots
  robot fisca
  variables
    id,fmax,f,av,ca:numero
  comenzar
    fmax:=0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    repetir 8
      Random(id,1,3)
      habilitarUnBot(id)
      Random(av,40,60)
      Random(ca,40,60)
      enviarEsquina(av,ca,id)
    EnviarMensaje(F,r1)
    EnviarMensaje(F,r2)
    EnviarMensaje(F,r3)
    repetir 3
      RecibirMensaje(f,*)
      fmax:=fmax+f
    Informar(fmax)
  fin
  robot florero
  variables
    ok:boolean
    f,av,ca,miav,mica,id:numero
  comenzar
    miav:=PosAv
    mica:=PosCa
    f:=0
    RecibirMensaje(id,rf)
    RecibirMensaje(ok,rf)
    mientras ok
      RecibirMensaje(av,rf)
      RecibirMensaje(ca,rf)
      BloquearEsquina(av,ca)
      Pos(av,ca)
      juntarFlores(f)
      Pos(miav,mica)
      LiberarEsquina(av,ca)
      RecibirMensaje(ok,rf)
    BloquearEsquina(10,10)
    Pos(10,10)
    repetir f
      depositarFlor
    Pos(miav,mica)
    LiberarEsquina(10,10)
    EnviarMensaje(f,rf)
  fin
variables
  r1,r2,r3:florero
  rf:fisca
comenzar
  AsignarArea(r1,c)
  AsignarArea(r2,c)
  AsignarArea(r3,c)
  AsignarArea(r1,c1)
  AsignarArea(r2,c1)
  AsignarArea(r3,c1)
  
  AsignarArea(r1,a1)
  AsignarArea(r2,a2)
  AsignarArea(r3,a3)
  AsignarArea(rf,a0)
  
  Iniciar(r1,1,1)
  Iniciar(r2,2,1)
  Iniciar(r3,3,1)
  Iniciar(rf,4,1)
fin