{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso procesarEsquina
  variables
    av,ca:numero
  comenzar
    av:=PosAv
    ca:=PosCa
    Pos(99,99)
    mientras HayPapelEnLaBolsa
      depositarPapel
    Pos(av,ca)
  fin
  proceso Izquierda
  comenzar
    repetir 3
      derecha
  fin
  proceso juntarPapeles( ES ok:boolean)
  comenzar
    si HayPapelEnLaEsquina
      ok:=V
      mientras HayPapelEnLaEsquina
        tomarPapel
    sino
      ok:=F
  fin
  proceso hacerEscalon(E tam:numero; ES cant:numero)
  variables
    ok:boolean
  comenzar
    repetir tam
      juntarPapeles(ok)
      si ok
        cant:=cant+1
      mover
  fin
areas
  c: AreaC(99,99,99,99)
  
  a0: AreaP(1,1,1,1)
  a1: AreaP(2,1,18,13)
  a2: AreaP(32,1,48,13)
  a3: AreaP(62,1,78,13)
robots
  robot Papelero
  variables
    cant,tam,av,ca:numero
    ok:boolean
  comenzar
    cant:=0
    repetir 4
      RecibirMensaje(ok,rj)
      Random(tam,1,3)
      hacerEscalon(tam,cant)
      BloquearEsquina(99,99)
      procesarEsquina
      LiberarEsquina(99,99)
      
      Random(tam,2,4)
      derecha
      hacerEscalon(tam,cant)
      BloquearEsquina(99,99)
      procesarEsquina
      LiberarEsquina(99,99)
      Izquierda
      EnviarMensaje(V,rj)
    EnviarMensaje(cant,rj)
  fin
  robot jefe
  variables
    sum,cant:numero
    ok:boolean
  comenzar  
    sum:=0
    repetir 4
      EnviarMensaje(V,r1)
      EnviarMensaje(V,r2)
      EnviarMensaje(V,r3)
      
      RecibirMensaje(ok,r1)
      RecibirMensaje(ok,r2)
      RecibirMensaje(ok,r3)
    Pos(99,99)
    mientras HayPapelEnLaEsquina
      tomarPapel
    repetir 3
      RecibirMensaje(cant,*)
      sum:=cant+sum
    Informar(sum)
  fin
variables
  r1,r2,r3:Papelero
  rj:jefe
comenzar
  AsignarArea(r1,c)
  AsignarArea(r2,c)
  AsignarArea(r3,c)
  AsignarArea(rj,c)
  
  AsignarArea(r1,a1)
  AsignarArea(r2,a2)
  AsignarArea(r3,a3)
  AsignarArea(rj,a0)
  
  Iniciar(r1,2,1)
  Iniciar(r2,32,1)
  Iniciar(r3,62,1)
  Iniciar(rj,1,1)
  
fin